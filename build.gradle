buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.blamejared.com' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'com.blamejared', name: 'ModTemplate', version: '2.+', changing: true
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.blamejared.modtemplate'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = com.blamejared.modtemplate.Utils.updatingVersion('2.0.0')
group = 'com.blamejared.jeitweaker'
archivesBaseName = 'JEITweaker-1.18.1'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven"
        content {
            includeGroup("mezz.jei")
        }
    }
    maven {
        url "https://maven.blamejared.com"
        content {
            includeGroup("com.blamejared.crafttweaker")
            includeGroup("org.openzen.zencode")
        }
    }
}
minecraft {
    mappings channel: 'official', version: '1.18.1'
    runs {
        client {
            workingDirectory project.file('run')

            mods {
                jeitweaker {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')

            mods {
                jeitweaker {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'jeitweaker', '--all', '--output', file('src/generated/resources/')

            mods {
                jeitweaker {
                    source sourceSets.main
                }
            }
        }
    }

    runs.configureEach {
        property 'mixin.env.remapRefMap', 'true'
        property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
    }
}

modTemplate {
    mcVersion "1.18.1"
    curseHomepage "https://www.curseforge.com/minecraft/mc-mods/jeitweaker"
    displayName 'JEITweaker'

    changelog {
        enabled true
        firstCommit "12c42310f5b56b51ce36a5d1c7e6fdc00ead98c9"
        repo "https://github.com/CraftTweaker/JEITweaker"
    }
    versionTracker {
        enabled true
        author "Jared"
        projectName "JEITweaker"
    }
    webhook {
        enabled true
        curseId "368718"
        avatarUrl "https://media.forgecdn.net/avatars/255/940/637203420213364761.png"
    }
}

dependencies {
    def mcDep = minecraft 'net.minecraftforge:forge:1.18.1-39.0.18'
    def ctDep = fg.deobf('com.blamejared.crafttweaker:CraftTweaker-forge-1.18.1:9.0.0+15')

    implementation ctDep
    compileOnly 'mezz.jei:jei-1.18.1:9.2.1.69:api'
    runtimeOnly fg.deobf('mezz.jei:jei-1.18.1:9.2.1.69')

//    annotationProcessor 'com.blamejared.crafttweaker:Crafttweaker_Annotation_Processors-1.17.1:2.0.0.15'
//    annotationProcessor 'com.google.code.gson:gson:2.8.6'
//    annotationProcessor 'org.reflections:reflections:0.9.10'
//    annotationProcessor mcDep
//    annotationProcessor ctDep
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "JEITweaker",
                "Specification-Vendor"    : "BlameJared",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${archiveVersion}",
                "Implementation-Vendor"   : "BlameJared",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

javadoc {
    include(
            'com/blamejared/jeitweaker/api/**',
            'com/blamejared/jeitweaker/bridge/JeiCategoryPluginBridge.java',
            'com/blamejared/jeitweaker/bridge/package-info.java',
            'com/blamejared/jeitweaker/zen/JeiManager.java',
            'com/blamejared/jeitweaker/zen/package-info.java',
            'com/blamejared/jeitweaker/zen/category/JeiCategory.java',
            'com/blamejared/jeitweaker/zen/category/SimpleJeiCategory.java',
            'com/blamejared/jeitweaker/zen/category/package-info.java',
            'com/blamejared/jeitweaker/zen/component/**',
            'com/blamejared/jeitweaker/zen/recipe/RecipeGraphics.java',
            'com/blamejared/jeitweaker/zen/recipe/package-info.java'
    )
    options.tags = [
            'apiNote:a:Api Note:',
            'implSpec:a:Implementation Specifications:',
            'implNote:a:Implementation Note:'
    ]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Creates a JAR containing the source code.'
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Creates a JAR containing the JavaDocs.'
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar) {
    description = 'Creates a JAR containing the non-obfuscated compiled code.'
    from sourceSets.main.output
    classifier = "deobf"
}
artifacts {
    archives sourcesJar
    archives javadocJar
    archives deobfJar
}

publish.dependsOn(project.tasks.getByName("assemble"))
publish.mustRunAfter(project.tasks.getByName("build"))

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }

            artifact sourcesJar {

                classifier 'sources'
            }
            artifact javadocJar {

                classifier 'javadoc'
            }
            artifact deobfJar {

                classifier 'deobf'
            }
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }
}

// Disables Gradle's custom module metadata from being published to maven. The
// metadata includes mapped dependencies which are not reasonably consumable by
// other mod developers.
tasks.withType(GenerateModuleMetadata) {

    enabled = false
}

curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    project {
        id = "368718"
        releaseType = 'release'
        changelog = file("changelog.md")
        changelogType = 'markdown'
        addGameVersion '1.18.1'

//        addArtifact(sourcesJar)
//        addArtifact(javadocJar)
        addArtifact(deobfJar)
    }
}

